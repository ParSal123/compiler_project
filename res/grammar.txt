Program -> DecList eof .

DecList -> #push TypeSpec #push id Fid_4 DecList 
            | .

Fid_4 -> ; #decl_var
        | [ #push num ] ; #decl_arr
        | ( #decl_func Params ) CompoundStmt .

TypeSpec -> int 
           | void .

Params -> #push int #push id Fid_2 ParamList 
         | void Fvoid .

Fvoid -> #error_void_param id Fid_2 ParamList 
        | .

ParamList -> , #push TypeSpec #push id Fid_2 ParamList 
              | .

Fid_2 -> #add_param_int
        | [ ] #add_param_arr .

CompoundStmt -> { DecList StmtList } .

StmtList -> Stmt StmtList 
             | .

Stmt -> #decl_normal_scope CompoundStmt 
       | if ( Expr ) #decl_if_scope Stmt else #decl_else_scope Stmt #end_else_scope
       | while #label ( Expr ) #decl_while_scope Stmt #end_while_scope
       | return #return_type_check Freturn #return_assignment
       | switch ( Expr ) { #decl_switch_scope CaseStmts DefaultStmt }
       | continue #continue ;
       | break #break ;
       | Freturn .

Freturn -> ; 
          | Expr ; #pop .

CaseStmts -> CaseStmt CaseStmts 
              | .

CaseStmt -> case num : StmtList .

DefaultStmt -> default : StmtList 
              | .

Expr -> #push id Fid_5 
       | ( Expr ) Term_1 AddExpr_1 FAddExpr 
       | #push num Term_1 AddExpr_1 FAddExpr 
       | + Factor Term_1 AddExpr_1 FAddExpr 
       | - Factor #negate Term_1 AddExpr_1 FAddExpr .

Fid_5 -> Fid F]
        | #push ( Args ) #func_call Term_1 AddExpr_1 FAddExpr .

F] -> = Expr #assign
     | Term_1 AddExpr_1 FAddExpr .

Fid -> 
      | [ Expr ] #resolve_array_index .

FAddExpr -> #push Relop Term AddExpr_1 #relop
           | .

Relop -> < 
        | == .

AddExpr_1 -> + Term #add AddExpr_1 
            | - Term #sub AddExpr_1
            | .

Term -> SignedFactor Term_1 .

Term_1 -> * SignedFactor #mult Term_1 
         | .

SignedFactor -> Factor 
               | + Factor 
               | - Factor #negate .

Factor -> ( Expr ) 
         | #push id Fid_3 
         | #push num .

Fid_3 -> Fid 
        | #push ( Args ) #func_call .

Args -> Expr ArgList_1 
       | .

ArgList_1 -> , Expr ArgList_1 
            | .
